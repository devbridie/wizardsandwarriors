group = "com.devbridie.wizardsandwarriors"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = '1.2.10'
    ext.dokka_version = '0.9.15'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'gfm'
    outputDirectory = "docs"
    moduleName = ""
    kotlinTasks {
        includes = ['packages.md']
        defaultKotlinTasks() + [project("dsl").compileKotlin, project("framework").compileKotlin, project("sample").compileKotlin]
    }
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: 'org.junit.platform.gradle.plugin'

    junitPlatform {
        enableStandardTestTask true

        filters {
            engines {
                include 'spek'
            }
        }
    }


    repositories {
        mavenCentral()
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testCompile 'org.jetbrains.spek:spek-api:1.1.5'
        testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
        testCompile 'io.kotlintest:kotlintest:2.0.7'
    }
}